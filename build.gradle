// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: "gradlescripts/versions.gradle"

    dependencies {
        classpath("com.applovin.quality:AppLovinQualityServiceGradlePlugin:5.1.7")
    }
}

plugins {
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.jetbrainsKotlinAndroid) apply false
    alias(libs.plugins.androidLibrary) apply false
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}

tasks.register("printSdkVersion") {
    group = "Release sdk"
    description = "Print apps tools sdk version"
    doLast {
        println SDK_VER
    }
}

tasks.register("releaseDevelopementSdk") {
    group = "Release sdk"
    description = "Release RC apps tools sdk"
    doLast {
        def version = "v" + SDK_VER + ".rc"
        // Create a Git tag
        exec {
            commandLine "git", "tag", version
        }
        exec {
            commandLine "git", "push", "origin", version
        }
    }
}
tasks.register("releaseSdk") {
    group = "Release sdk"
    description = "Release apps tools sdk"
    doLast {
        def version = "v" + SDK_VER
        // Check if the tag already exists
        def tagExists = exec {
            commandLine "git", "rev-parse", "--quiet", "--verify", "refs/tags/${version}"
            ignoreExitValue true
        }.exitValue == 0

        if (tagExists) {
            throw new Exception("Error: Tag ${version} already exists. Please update the SDK and " +
                    "adapters version inside the versions.gradle file before each new release.")
        }

        // Create a Git tag
        exec {
            commandLine "git", "tag", version
        }
        exec {
            commandLine "git", "push", "origin", version
        }
    }
}
